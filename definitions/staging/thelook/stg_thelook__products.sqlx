
config {
  type: "view",
  schema: "staging",
  tags: ["staging", "thelook"],
  description: "Cleaned and standardised products. One row per product. Direct 1-to-1 with the source table. No joins.",
  columns: {
    product_id:             "Primary key. Renamed from id.",
    cost:                   "Cost of goods sold (COGS). Defaults to 0 when NULL in source.",
    category:               "Normalised product category (lowercase).",
    product_name:           "Product display name. Renamed from name to avoid ambiguity. Defaults to 'Unknown' when NULL in source.",
    brand:                  "Brand name of the product.",
    retail_price:           "Standard retail price. Defaults to 0 when NULL in source.",
    department:             "Normalised department (lowercase).",
    sku:                    "Stock keeping unit identifier.",
    distribution_center_id: "Foreign key to stg_thelook__distribution_centers."
  },
  assertions: {
    uniqueKey: ["product_id"],
    nonNull:   ["product_id", "product_name", "retail_price", "cost"]
  }
}

SELECT
  id                    AS product_id,
  COALESCE(cost, 0)         AS cost,
  LOWER(category)       AS category,
  COALESCE(name, 'Unknown') AS product_name,
  brand,
  COALESCE(retail_price, 0) AS retail_price,
  LOWER(department)     AS department,
  sku,
  distribution_center_id

FROM ${ref("products")}
